# RT-Thread building script for component

import os
from building import *

Import('rtconfig')

if os.getenv('BSP_ROOT'):
    BSP_ROOT = os.getenv('BSP_ROOT')
else:
    BSP_ROOT = os.path.normpath(os.getcwd() + '../../../../../../')

cwd     = GetCurrentDir()
src     = Glob('*.c') + Glob('*.cpp')
CPPPATH = [cwd, BSP_ROOT]

ASFLAGS = ''
for path in CPPPATH:
    ASFLAGS = ASFLAGS + ' -I ' + path

if rtconfig.PLATFORM in ['armcc', 'armclang']:
    src += Glob('*_rvds.S')

if rtconfig.PLATFORM in ['gcc']:
    src += Glob('*_init.S')
    src += Glob('*_gcc.S')

if rtconfig.PLATFORM in ['iccarm']:
    src += Glob('*_iar.S')

if not GetDepend('RT_USING_MEM_PROTECTION') and not GetDepend('RT_USING_HW_STACK_GUARD'):
    SrcRemove(src, 'mpu.c')

group = DefineGroup('CPU', src, depend = [''], CPPPATH = CPPPATH, ASFLAGS = ASFLAGS)

Return('group')
