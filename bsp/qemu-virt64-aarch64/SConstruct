import os
import sys
import rtconfig
import re
import subprocess

if os.getenv('RTT_ROOT'):
    RTT_ROOT = os.getenv('RTT_ROOT')
else:
    RTT_ROOT = os.path.join(os.getcwd(), '..', '..')


sys.path = sys.path + [os.path.join(RTT_ROOT, 'tools')]
from building import *

TARGET = 'rtthread.' + rtconfig.TARGET_EXT
TRACE_CONFIG = ""
USING_VDSO = False
content = ""
with open("rtconfig.h") as f:
    for line in f.readlines():
        if line.find("RT_BACKTRACE_FUNCTION_NAME") != -1:
            for token in line.split(" "):
                if re.match(r'RT_BACKTRACE_FUNCTION_NAME$', token, flags=0):
                    TRACE_CONFIG = " "
        if line.find("RT_USING_VDSO") != -1:
            USING_VDSO = True

DefaultEnvironment(tools=[])
env = Environment(tools = ['mingw'],
    AS   = rtconfig.AS, ASFLAGS = rtconfig.AFLAGS + TRACE_CONFIG,
    CC   = rtconfig.CC, CFLAGS = rtconfig.CFLAGS + TRACE_CONFIG,
    CXX  = rtconfig.CXX, CXXFLAGS = rtconfig.CXXFLAGS  + TRACE_CONFIG,
    AR   = rtconfig.AR, ARFLAGS = '-rc',
    LINK = rtconfig.LINK, LINKFLAGS = rtconfig.LFLAGS)
env.PrependENVPath('PATH', rtconfig.EXEC_PATH)
env['ASCOM'] = env['ASPPCOM']
env['LINKCOM'] = '$LINK -o $TARGET $LINKFLAGS $__RPATH $SOURCES $_LIBDIRFLAGS -Wl,--start-group $_LIBFLAGS -Wl,--end-group'

if USING_VDSO:
    vdso_file = os.path.join(RTT_ROOT, 'components', 'vdso', 'usr', 'xmake.lua')
    vdso_so = os.path.join(RTT_ROOT, 'components', 'vdso', 'usr', 'build')
    command = ["xmake", "-F", vdso_file]
    result = subprocess.run(command)
    if result.returncode == 0:
        print("Command executed successfully")
    else:
        print("Command failed with exit code:", result.returncode)
        exit(1)

    clear = "rm -rf {vdso_path}".format(vdso_path=vdso_so)
    env.Command(
        target = "build_vdso",
        source = "",
        action = [clear]
    )

Export('RTT_ROOT')
Export('rtconfig')

# prepare building environment
objs = PrepareBuilding(env, RTT_ROOT)

# make a building
DoBuilding(TARGET, objs)
