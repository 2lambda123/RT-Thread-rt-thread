from building import *
import rtconfig

cwd = GetCurrentDir()
src = ['rt_spi_core.c', 'rt_spi_dev.c']
CPPPATH = [cwd, cwd + '/../include']
LOCAL_CCFLAGS = ''

if GetDepend('RT_USING_QSPI'):
    src += ['rt_qspi_core.c']

src_device = []

if GetDepend('RT_USING_SPI_WIFI'):
    src_device += ['rt_spi_wifi_rw009.c']

if GetDepend('RT_USING_ENC28J60'):
    src_device += ['rt_enc28j60.c']

if GetDepend('RT_USING_SPI_MSD'):
    src_device += ['rt_spi_msd.c']

if GetDepend('RT_USING_SFUD'):
    src_device += ['rt_spi_flash_sfud.c', 'sfud/src/rt_sfud.c']
    CPPPATH += [cwd + '/sfud/inc']
    if GetDepend('RT_SFUD_USING_SFDP'):
        src_device += ['sfud/src/rt_sfud_sfdp.c']
    if rtconfig.CROSS_TOOL == 'gcc':
        LOCAL_CCFLAGS += ' -std=c99'
    elif rtconfig.CROSS_TOOL == 'keil':
        LOCAL_CCFLAGS += ' --c99'

src += src_device

group = DefineGroup('DeviceDrivers', src, depend = ['RT_USING_SPI'], CPPPATH = CPPPATH, LOCAL_CCFLAGS = LOCAL_CCFLAGS)

Return('group')
